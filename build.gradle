plugins {
  id 'java'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'jasperreports:jasperreports:+'
}

sourceSets {
  main {
    java {
      srcDirs 'src'
    }
    resources {
      srcDirs 'resources'
      exclude 'jrxml'
    }
  }
}

task copyLibs(type: Copy) {
  doFirst {
    println "Copying JAR libraries..."
  }
  eachFile {
    println it.name
  }
  from configurations.runtimeClasspath
  into "${jar.destinationDirectory.get()}/lib"
}

ant.taskdef(name: 'compileJrxml',
            classname: 'net.sf.jasperreports.ant.JRAntCompileTask',
            classpath: configurations.runtimeClasspath.asPath)

jar {
  dependsOn copyLibs
  doFirst {
    def srcdir = "resources/jrxml"
    def destdir = mkdir("resources/jasper").path
    ant.compileJrxml(srcdir: srcdir,
                     destdir: destdir)
  }
  manifest {
    attributes 'Main-Class': 'GUI.Main',
               'Class-Path':configurations.runtimeClasspath
                              .collect { "lib/$it.name" }.join(' ')
  }
}
